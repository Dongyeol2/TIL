# OPA(Open Policy Agent)



## Introduction

-----

### Overview

- 정책을 **결정**하는 부분과 **수행**하는 영역을 분리하기위해 시작 ([Why decoupling?](#정책-분리란-무엇인가?))
- 전체 시스템에 걸쳐 통합된 상황에 따른 정책 시행을 가능하게 하는 오픈 소스 범용 정책 엔진
- 소프트웨어가 정책 결정을 해야 할 때 OPA에 질의하고 구조화된 데이터(예: JSON)를 입력으로 제공
- OPA는 입력으로 임의의 구조화된 데이터를 받을 수 있다.
- OPA는 현재 클라우드 네이티브 정책의 사실상 표준으로 자리잡음 

![OPA](https://github.com/user-attachments/assets/e0e6a496-6eb3-4360-b7fa-4234f9af0851)

OPA는 정책 및 데이터에 대한 질의를 평가하여 정책의 결정을 돌려준다.
**질의(Query)**와 **결정(Decision)**은 **json**으로 표현하면 어떤 것이든 포함할 수 있으며 이에따라 매우 큰 확장성을 갖는다.

정책은 **전용 언어(Rego)**를 사용하여 기술하는데 도메인에 제한 받지 않는 (거의) 모든 종류의 정책의 기술이 가능하다.

질의의 예는 다음과 같다.

- 어떤 사용자가 어떤 리소스에 액세스할 수 있는가?
- 어떤 서브넷에 외부통신이 허용되는가?
- 어떤 클러스터로 워크로드를 배포해야 할 것인가?
- 어떤 레지스트리로부터 바이너리를 다운로드할 수 있는가?
- 어떤 OS 기능이 컨테이너와 함께 실행되는가?
- 어떤 시간에 시스템에 접근할 수 있는가?

정책 결정은 단순히 예/아니오나 허용/거부와 같은 답변에 한정되지 않으며, 입력과 마찬가지로 임의의 구조화된 데이터를 출력으로 생성할 수 있다.

데이터는 **Json 형태로 저장**되며 OPA 서버는 **정책(Policy)을 기반으로 판단**하는데 질의와 함께 이 데이터를 활용하여 결정을 만든다.



### 가상시나리오

다음 그림과 같은 시스템이 있다고 가정.

1. 0 개 이상의 프로토콜을 받는 서버들이 존재
2. 공용이나 사설망에 서버들을 연결하는 네트워크
3. 네트워크에 서버를 연결시키는 포트

![OPA_scenario1](https://github.com/user-attachments/assets/56d62a09-ecbf-4fea-9574-6900004ad6a1)



![OPA_scenario2](https://github.com/user-attachments/assets/1fa270f5-1d00-4bc9-84f2-8999b7357179)

위 그림의 시스템은 다음과 같이 json으로 표현할 수 있다.

```json
{
    "servers": [
        {"id": "app", "protocols": ["https", "ssh"], "ports": ["p1", "p2", "p3"]},
        {"id": "db", "protocols": ["mysql"], "ports": ["p3"]},
        {"id": "cache", "protocols": ["memcache"], "ports": ["p3"]},
        {"id": "ci", "protocols": ["http"], "ports": ["p1", "p2"]},
        {"id": "busybox", "protocols": ["telnet"], "ports": ["p1"]}
    ],
    "networks": [
        {"id": "net1", "public": false},
        {"id": "net2", "public": false},
        {"id": "net3", "public": true},
        {"id": "net4", "public": true}
    ],
    "ports": [
        {"id": "p1", "network": "net1"},
        {"id": "p2", "network": "net3"},
        {"id": "p3", "network": "net2"}
    ]
}
```

아래와 같은 요구사항이 있고 이에따라 정책을 적용해야한다고 가정

```
1. 인터넷에서 접속 가능한 서버들은 http를 노출해서는 안됨.
2. 모든 서버들은 telnet을 노출해서는 안됨.
```

이를 rego를 사용하여 정의하면 다음과 같다. ([Policy Language](#policy-language))
rego 공식 문서 (https://www.openpolicyagent.org/docs/latest/policy-language/)

```py
package example

default allow = false                               # 기본적으로 허용되지 않는다.

allow = true {                                      # 위반이 하나도 없다면 허용한다.
    count(violation) == 0
}

violation[server.id] {                              # 서버는 다음의 경우에 위반
    some server                                     # 모든 서버를 대상으로
    public_server[server]                           # public_server 함수를 통해 설정되어 있거나
    server.protocols[_] == "http"                   # http 프로토콜을 포함하고 있다면
}

violation[server.id] {                              # 서버는 다음의 경우에 위반 (앞의 룰과 or조건)
    server := input.servers[_]                      # input에 있는 모든 서버들에 대해
    server.protocols[_] == "telnet"                 # telnet 프로토콜을 포함하고 있다면
}

public_server[server] {                             # 서버가 public에 연결되었는지 체크
    some i, j
    server := input.servers[_]                      # input에 있는 모든 서버를 대상으로
    server.ports[_] == input.ports[i].id            # 모든 포트들의 id를 기준으로
    input.ports[i].network == input.networks[j].id  # 그 포트의 네트워크를 찾아서 
    input.networks[j].public                        # 그 네트워크가 public 이면
}
```





## Philosophy(철학)

----

정책은 소프트웨어 서비스의 동작을 규제하는 규칙 집합으로, 속도 제한, 신뢰할 수 있는 서버의 이름, 애플리케이션이 배포되어야 할 클러스터, 허용된 네트워크 경로, 사용자가 돈을 인출할 수 있는 계정 등을 정의할 수 있다.

**인증(Authorization)**은 특정 사람이나 기계가 어떤 리소스에서 어떤 작업을 실행할 수 있는지를 규정하는 **특별한 형태의 정책**이다. 인증(Authorization)은 종종 **인증(Authentication)**과 혼동되는데, 인증(Authentication)은 사람들이나 기계가 자신을 증명하는 방법이다. 인증(Authorization)과 더 일반적으로 정책은 종종 사용자 이름, 속성, 그룹, 클레임 등 인증 결과를 활용하지만, 단순히 사용자 신원에만 의존하지 않고 더 다양한 정보를 기반으로 결정한다. 정책을 일반화해보면, 모든 정책이 사용자를 다루는 것은 아니며, 예를 들어 "모든 바이너리는 신뢰할 수 있는 소스에서 와야 한다"와 같은 시스템 내에서 유지되어야 하는 **불변 조건도 정책에 포함**된다.

현재는 정책이 실제로 규제하는 소프트웨어 서비스에 하드코딩되어 있는 경우가 많다. Open Policy Agent(OPA)는 이러한 정책을 **소프트웨어 서비스와 분리**할 수 있게 해 준다. 이를 통해 정책 책임자는 정책을 읽고, 작성하고, 분석하고, 버전 관리하고, 배포하며, 전반적으로 소프트웨어 서비스와 **독립적으로 관리**할 수 있다. OPA는 소프트웨어 서비스와 정책을 분리하고, 다양한 문맥(context)을 고려한 정책을 작성할 수 있는 통합 도구 세트를 제공한다. 즉, OPA는 다양한 문맥을 활용해 어떤 소프트웨어 시스템이든 정책을 분리하는 데 도움을 준다.

### What is Policy ?

정책은 조직이 법적 요구사항, 기술적 제약 등을 준수하고 실수를 피하기 위한 필수적인 지침이다. 정책은 수동으로 적용되거나 애플리케이션 로직 및 배포 시의 정적 설정을 통해 실행될 수 있다.

### What is Policy Decoupling ?

  Policy Decoupling이란 소프트웨어 서비스에서 **정책을 선언적으로 정의**하고, 컴파일이나 배포 없이 **언제든 업데이트**할 수 있으며 **자동으로 적용되도록 하는 개념**이다. 

  이는 특히 **빠른 의사결정**이 필요한 경우에 유용하다. 정책의 분리를 통해 소프트웨어는 **대규모로 확장 가능**하며, 변화하는 비즈니스 요구에 적응하고, **위반 사항이나 충돌을 발견**하기 쉽고, 정책 준수의 **일관성**을 높이며, **인적 오류의 위험**을 줄일 수 있다. 

  이 방식은 개발 당시 예측할 수 없었던 외부 환경 변화에도 정책이 쉽게 적응할 수 있도록 해준다. 예를 들어, 클라우드 서비스는 **추가적인 컴퓨팅 용량을 사용할 수 있는지**, **어떤 지역에서 용량을 추가할 수 있는지**, **잘못된 지역에서 실행 중인 인스턴스는 무엇인지** 같은 질문에 답할 수 있다.

### What is OPA ?

OPA는 서비스에 함께 배치할 수 있는 **경량 정책 엔진**으로, 사이드카, 데몬, 또는 라이브러리 형태로 통합된다. 서비스는 OPA에 질의하여 **정책 결정**을 위임하며, OPA는 **정책과 데이터를 평가해 결과를 반환**한다. 정책은 고수준의 언어로 작성되어 API나 파일 시스템을 통해 **동적으로 로드**될 수 있다.

### Why use OPA ?

OPA는 소프트웨어의 정책 결정을 위임할 수 있는 강력한 정책 엔진으로, 정책 구현을 처음부터 개발할 필요 없이 **관리 및 제어를 쉽게 할 수 있도록 지원**한다. 이를 통해 설계, 구현, 보안, 유지 보수 등에 드는 상당한 노력을 줄일 수 있다.

### The OPA Document Model

OPA(오픈 정책 에이전트) 정책은 **계층적 구조의 데이터를 기반으로 의사 결정**을 하며, 이는 '문서', '속성 집합', '컨텍스트' 또는 **'JSON'**이라 불리기도 한다. OPA는 특정 도메인 모델에 묶이지 않고, 임의의 구조화된 데이터를 사용하여 정책을 평가할 수 있다. OPA의 정책은 boolean, 문자열, 맵, 리스트 등의 형식으로 **구조화된 데이터를 결과로 반환**할 수 있다.

OPA는 외부에서 데이터를 **동기적 또는 비동기적**으로 가져오는 인터페이스를 통해 데이터를 불러올 수 있으며, 이를 '**기본 문서(base documents)**'라고 부른다. **이러한 문서들은 정책 결정에 활용**된다. 또한, OPA 내의 정책은 **다른 정책을 참조**할 수 있으며, 이를 통해 생성된 값을 '**가상 문서(virtual documents)**'라고 한다. 가상 문서는 정책에 의해 계산되며, 외부에서 불러온 것이 아니다.

기본 문서와 가상 문서는 동일한 유형의 정보(숫자, 문자열, 리스트 등)를 표현할 수 있으며, Rego 언어는 두 문서 모두 동일한 구문으로 참조할 수 있다. 정책 작성자는 'data'라는 전역 변수를 통해 기본 및 가상 문서를 참조하며, OPA의 /v1/data HTTP API를 통해 질의할 수 있다.

**기본 문서의 로딩 및 참조 방법**:

- **비동기 로딩**: 외부 데이터가 변경되면 주기적 또는 이벤트 발생 시 OPA로 복제된다. 이 경우 'data' 전역 변수를 통해 참조된다.
- **동기 로딩**: 정책 평가 시 소프트웨어가 OPA에 질의할 때 'input'이라는 전역 변수로 전달되며, 이는 데이터 명명 충돌을 방지하기 위해 'data' 외부에서 관리된다.

OPA는 기본 및 가상 문서의 통합된 참조 방식과 정책 작성의 일관성을 제공하여 복잡한 정책 구현을 단순화하고, 다양한 외부 데이터와 정책 간의 연계를 용이하게 한다.

다음 표에는 기본 문서를 OPA에 로드하기 위한 다양한 모델, 정책 내에서 참조할 수 있는 방법 및 로드를 위한 실제 메커니즘이 요약되어 있다.

| Model             | How to access in Rego                                        | How to integrate with OPA                                    |
| ----------------- | ------------------------------------------------------------ | ------------------------------------------------------------ |
| Asynchronous Push | The `data` global variable                                   | Invoke OPA’s API(s), e.g., `PUT /v1/data`                    |
| Asynchronous Pull | The `data` global variable                                   | Configure OPA’s [Bundle](https://www.openpolicyagent.org/docs/latest/management-bundles) feature |
| Synchronous Push  | The `input` global variable                                  | Provide data in policy query, e.g., inside the body of `POST /v1/data` |
| Synchronous Pull  | The [built-in functions](https://www.openpolicyagent.org/docs/latest/policy-reference), e.g., `http.send` | N/A                                                          |

OPA에 **비동기적으로 로드된 데이터**는 정책 평가 중에 효율적으로 읽을 수 있도록 **메모리 내에 캐시**된다. 마찬가지로 **정책도 메모리에 캐시**되어 **고성능과 고가용성을 보장**한다. **동기적으로 가져온 데이터**는 **메모리 내에서 캐시될 수도** 있다. 장단점을 포함하여 외부 데이터를 OPA에 로드하는 방법 - [External Data](#external-data)

가상 정책에 대한 기본 및 가상 문서 모델예시 다이어그램
data.acme.allow 기반으로 승인 결정을 내림.

- 동기적으로 push된 API 요청 정보는 input 아래에 위치
- 권한 데이터는 비동기적으로 가져와서 data.entitlements 아래에 위치
- http.send 내장 함수를 사용하여 정책 평가 중에 동기적으로 가져온 리소스 데이터.

![policy_document_diagram](https://github.com/user-attachments/assets/a8af9097-b896-47bd-bac4-2d9a38f76387)



## Policy Language

OPA는 구조화된 문서에 표현된 정보를 추론하기 위해 만들어졌다. 서비스와 해당 사용자가 게시하는 데이터는 OPA의 기본 쿼리 언어인 Rego를 사용하여 검사하고 변환할 수 있다.

### What is Rego ?

Rego는 잘 알려진 수십 년 된 쿼리 언어인 Datalog에서 영감을 받았다. Rego는 Datalog를 확장하여 **JSON과 같은 구조화된 문서 모델을 지원**합니다.

Rego 쿼리는 OPA에 저장된 데이터에 대한 주장이다. 이러한 쿼리는 시스템의 예상 상태를 위반하는 데이터 인스턴스를 열거하는 정책을 정의하는 데 사용할 수 있다.

### Why use Rego ?

읽고 쓰기 쉬운 정책을 정의.

중첩된 문서 참조를 위한 강력한 지원을 제공하고 쿼리가 정확하고 모호하지 않도록 보장하는 데 중점을 둔다.

Rego는 선언적이므로 정책 작성자는 쿼리 실행 방법보다는 반환해야 하는 쿼리에 집중할 수 있다. 이러한 쿼리는 명령형 언어의 쿼리보다 더 간단하고 간결하다.

선언적 쿼리 언어를 지원하는 다른 애플리케이션과 마찬가지로 OPA는 쿼리를 최적화하여 성능을 향상시킬 수 있다.

[Rego language rules 참조](https://www.openpolicyagent.org/docs/latest/policy-language/#the-basics)

## Policy Reference

[Rego 내장 라이브러리 참조](https://www.openpolicyagent.org/docs/latest/policy-reference/#assignment-and-equality)

## Policy Testing

OPA는 시스템의 중요한 요구 사항을 성문화하는 세분화된 정책을 작성할 수 있는 높은 수준의 선언적 언어(Rego)를 제공한다.

정책의 정확성을 확인하는 데 도움이 되도록 OPA는 정책에 대한 테스트를 작성하는 데 사용할 수 있는 프레임워크도 제공한다. 정책에 대한 테스트를 작성하면 새로운 규칙의 개발 프로세스 속도를 높이고 요구 사항이 발전함에 따라 규칙을 수정하는 데 걸리는 시간을 줄일 수 있다.

### Getting Started

아래 파일은 새로운 사용자를 생성하고 사용자가 자신의 프로필에 액세스할 수 있도록 허용하는 간단한 정책을 구현

```py
# example.rego - 정책 정의
package authz

import rego.v1		# rego에서 제공하는 기본 모듈을 가져오는 구문.

# allow if {...} : allow 라는 규칙을 정의하며, 특정 조건이 true일 때 정책 승인. */
/* input.path가 ["users"]이고 input.method가 "POST"일 때 allow 규칙이 참(true)이 된다. 즉, /users 경로로 POST 요청이 들어오면 허용. 
allow if {
	input.path == ["users"]
	input.method == "POST"
}

# input.path가 ["users", input.user_id]이고, input.method가 "GET"일 때 allow 규칙이 참(true)이 된다. 즉, 특정 사용자의 /users/{user_id} 경로로 GET 요청이 들어오면 허용된다. 이때, input.user_id는 입력된 사용자 ID와 경로의 마지막 부분이 일치해야 한다.
allow if {
	input.path == ["users", input.user_id]
	input.method == "GET"
}
```

```py
# example_test.rego - 테스트 정의
package authz_test

import rego.v1

import data.authz

# /users 경로로 POST 요청 시 allow 규칙이 true가 되는지 테스트. authz.allow를 호출하며, input이 {"path": ["users"], "method": "POST"}일 때 테스트가 성공하면 PASS. 
test_post_allowed if {
	authz.allow with input as {"path": ["users"], "method": "POST"}
}

# /users 경로로 GET 요청 시 allow 규칙이 false인지 테스트. 익명 사용자가 /users에 접근하는 것이 거부되는지 확인. 
test_get_anonymous_denied if {
	not authz.allow with input as {"path": ["users"], "method": "GET"}
}

# /users/bob 경로로 GET 요청 시, input.user_id가 "bob"일 때 allow 규칙이 true가 되는지 테스트. 즉, 사용자가 자신의 경로에 접근할 때 허용되는지 확인.  
test_get_user_allowed if {
	authz.allow with input as {"path": ["users", "bob"], "method": "GET", "user_id": "bob"}
}

# /users/bob 경로로 GET 요청 시, input.user_id가 "alice"일 때 allow 규칙이 false인지 테스트. 즉, 다른 사용자가 /users/bob 경로에 접근할 때 거부되는지 확인. 
test_get_another_user_denied if {
	not authz.allow with input as {"path": ["users", "bob"], "method": "GET", "user_id": "alice"}
}
```

두 파일은 모두 동일한 디렉터리에 저장

```bash
$ ls
example.rego      example_test.rego
```

정책을 실행하려면 파일이 포함된 디렉터리에서 opa test 명령을 실행

```bash
$ opa test . -v
data.authz_test.test_post_allowed: PASS (1.417µs)
data.authz_test.test_get_anonymous_denied: PASS (426ns)
data.authz_test.test_get_user_allowed: PASS (367ns)
data.authz_test.test_get_another_user_denied: PASS (320ns)
--------------------------------------------------------------------------------
PASS: 4/4
```

opa test 출력은 모든 test가 통과했음을 나타낸다.

### Enriched Test Report With Variable Values

복잡한 출력이 포함된 규칙을 테스트할 때 특정 표현식이 테스트에 실패하게 만든 상황에 대해 자세히 아는 것이 유용할 수 있다. **--var-values** 플래그를 사용하면 표현식에 사용된 변수 또는 참조의 값을 포함하여 테스트 규칙의 **실패를 초래한 정확한 표현식으로 테스트 보고서를 강화**할 수 있다.

```py
# 정책 utility module
package authz

import rego.v1

# allowed_actions 함수 정의. 
# 주어진 user가 data.actions 맵의 값에 포함된 경우 그 action을 반환. 
# data.actions[action]은 action 키에 대해 사용자가 포함되어 있는지를 확인하는 부분.
# 최종적으로 사용자가 수행할 수 있는 모든 action의 리스트를 반환.
allowed_actions(user) := [action |
	user in data.actions[action]
]
```

```py
# 테스트 파일
package authz_test

import data.authz
import rego.v1

# 모든 사용자가 read 권한을 가졌는지 확인.
test_allowed_actions_all_can_read if {
	users := ["alice", "bob", "jane"]
	r := ["alice", "bob"]			   
	w := ["jane"]
	p := {"read": r, "write": w}

	every user in users {
		"read" in authz.allowed_actions(user) with data.actions as p
	}
}
```

--var-values 플래그를 사용하여 테스트 실행

```bash
opa test . --var-values
FAILURES
--------------------------------------------------------------------------------
data.authz_test.test_allowed_actions_all_can_read: FAIL (904µs)

util_test.rego:13:
"read" in authz.allowed_actions(user) with data.actions as p
|                     |                          |
|                     |                          {"read": ["alice", "bob"], "write": ["jane"]}
|                     "jane"
["write"]

SUMMARY
--------------------------------------------------------------------------------
util_test.rego:
data.authz_test.test_allowed_actions_all_can_read: FAIL (904µs)
--------------------------------------------------------------------------------
FAIL: 1/1
```

`"jane"`이 `read` 권한을 갖지 않으므로 테스트가 실패했다. 테스트 보고서에 실패한 식과 해당 지역 변수 할당을 포함함으로써 어떤 주장과 매개 변수 조합이 테스트 실패를 초래했는지 **즉시** 알 수 있으므로 개발자가 문제 해결을 더 쉽게 할 수 있다.

### Test Format

테스트는 규칙 이름 앞에 _test가 붙는 규칙을 사용하여 표준 Rego 규칙으로 표현된다.
ex) mypackage_test

### Test Discovery

opa test 하위 명령은 명령줄에 전달된 Rego 파일에 있는 모든 테스트(즉, 접두사 _test가 붙은 규칙)를 실행

### Specifying Tests to Run

`--run/-r regex` 옵션을 지원하여 검색된 테스트 중 평가해야 할 테스트를 추가로 지정한다. 이 옵션은 [re2 syntax](https://github.com/google/re2/wiki/Syntax)를 지원한다.

### Test Results

테스트 규칙이 정의되지 않았거나 **참이 아닌 값**을 생성하는 경우 테스트 결과는 **FAIL**로 보고된다. 테스트에서 런타임 오류(예: 0으로 나누기 조건)가 발생하면 테스트 결과가 **ERROR**로 표시된다. **todo_** 접두사가 붙은 테스트는 **SKIPPED**로 보고된다. 그렇지 않으면 테스트 결과가 **PASS**로 표시된다.

```py
# pass_fail_error_test.rego
package example_test

import rego.v1

import data.example

# This test will pass.
test_ok if true

# This test will fail.
test_failure if 1 == 2

# This test will error.
test_error if 1 / 0

# This test will be skipped.
todo_test_missing_implementation if {
    example.allow with data.roles as ["not", "implemented"]
}
```

### Data and Function Mocking

OPA의 `with` 키워드는 데이터 문서를 대체하거나 모의 객체로 함수를 호출하는 데 사용할 수 있다. 기본 문서와 가상 문서를 모두 교체할 수 있다.

내장 기능 또는 기타 기능을 교체할 때 다음과 같은 제약 조건이 적용된다.

1. `Internal.*` 함수, `rego.metadata.*` 또는 `eq;`를 대체한다. 또는 관계(`walk`)는 허용되지 않습니다.
2. Replacement 와 replaced function은 동일한 계수를 가져야 한다.
3. 대체된 함수는 대체하는 함수를 호출할 수 있으며, 이러한 호출은 원래 함수를 호출하며 재귀를 발생시키지 않는다.

example 1 ) 데이터 문서에 따라 달라지는 간단한 정책

```py
# authz.rego - 정책 모듈
package authz

import rego.v1

allow if {
    some x in data.policies
    x.name == "test_policy"
    matches_role(input.role)	#input.role이 mathces_role 규칙을 만족하는지 check
}

# my_role 역할에 대해 input.user가 data.roles[my_role] 리스트에 포함되어 있는지 확인
# data.roles는 외부 데이터로 제공되는 역할-사용자 매핑
matches_role(my_role) if input.user in data.roles[my_role] 
```

```py
# authz_test.rego - 테스트 모듈
package authz_test

import rego.v1

import data.authz

policies := [{"name": "test_policy"}] # 테스트에서 사용할 정책 데이터
roles := {"admin": ["alice"]}		 # 역할-사용자 매핑 데이터 

test_allow_with_data if {
    authz.allow with input as {"user": "alice", "role": "admin"}
        with data.policies as policies
        with data.roles as roles
}
```

```bash
$ opa test -v authz.rego authz_test.rego
data.authz_test.test_allow_with_data: PASS (697ns)
--------------------------------------------------------------------------------
PASS: 1/1
```

테스트는 `authz.allow` 규칙이 `{"user": "alice", "role": "admin"}` 입력과 `policies` 및 `roles` 데이터로 평가될 때 `true`를 반환하는지를 확인. `alice`는 `admin` 역할에 포함되어 있으며, `policies` 데이터에 `"test_policy"`가 있으므로 모든 조건이 만족되어 테스트 통과.

example 2 ) 인수 없이 규칙을 바꾸는 예시

```py
# authz.rego
package authz

import rego.v1

allow1 if allow2 # allow1 은 allow2가 true 일때만 true를 반환

allow2 if 2 == 1 # 2 == 1 항상 false이므로 기본적으로 false 반환
```

```py
# authz_test.rego
package authz_test

import rego.v1

import data.authz

test_replace_rule if {
    # allow1 규칙을 호출하여 테스트하되, allow2의 결과를 true로 대체하여 allow1의 동작을 평가
	authz.allow1 with authz.allow2 as true 
}
```

```bash
$  opa test -v authz.rego authz_test.rego
data.authz_test.test_replace_rule: PASS (328ns)
--------------------------------------------------------------------------------
PASS: 1/1
```

이 테스트는 `with` 구문을 통해 정책 규칙을 **동적으로 대체하여 테스트**할 수 있음을 보여준다.

example 3 ) 규칙의 내장함수를 사용자 정의 함수로 바꾸는 예

```py
#authz.rego
package authz

import rego.v1

import data.jwks.cert # JSON Web Key Set(jwks)의 인증서를 가져온다.

allow if {
    # io.jwt.decode_verify 함수로 input.headers["x-token"]을 인증서(cert)와 발행자(iss) 정보로 검증.
    # 결과가 [true, _, _]일 경우에만 allow가 true가 되어 요청이 승인.
	[true, _, _] = io.jwt.decode_verify(input.headers["x-token"], {"cert": cert, "iss": "corp.issuer.com"})
}
```

```py
#authz_test.rego
package authz_test

import rego.v1

import data.authz

# JWT 토큰 검증을 위해 io.jwt.decode_verify 함수를 대체하는 모의 함수
# "my-jwt" 토큰을 받을 때는 [true, {}, {}]을 반환하여 검증 성공
# 다른 토큰을 받을 때는 [false, {}, {}]을 반환하여 검증 실패
mock_decode_verify("my-jwt", _) := [true, {}, {}]
mock_decode_verify(x, _)        := [false, {}, {}] if x != "my-jwt"

# version 1
test_allow if {
    # authz.allow 규칙을 호출하고 input.headers["x-token"]을 "my-jwt"로 설정합니다.
    # data.jwks.cert는 "mock-cert"로 대체
    # io.jwt.decode_verify는 mock_decode_verify로 대체
	authz.allow with input.headers["x-token"] as "my-jwt"
		with data.jwks.cert as "mock-cert"
		with io.jwt.decode_verify as mock_decode_verify
}
    
# version 2 - 함수를 값으로 대체할 수도 있다
test_allow_value if {
    authz.allow 
    	with input.headers["x-token"] as "my-jwt"
    	with data.jwks.cert as "mock-cert"
    	with io.jwt.decode_verify as [true, {}, {}]    
}
```

```bash
$  opa test -v authz.rego authz_test.rego
data.authz_test.test_allow: PASS (458.752µs)
--------------------------------------------------------------------------------
PASS: 1/1
```

`mock_decode_verify` 함수가 `"my-jwt"` 입력에 대해 `true`를 반환하여 `authz.allow` 규칙이 `true`를 반환하였기 때문에 테스트가 통과되었다.

테스트는 JWT 검증 로직을 모의 함수로 대체하여 예상된 토큰 `"my-jwt"`에 대해 검증 성공을 확인
이 테스트는 JWT 검증과 인증서 확인을 포함하는 정책 로직을 안전하게 테스트할 수 있는 방법을 보여준다.

example 4 ) 하나의 내장 함수를 다른 내장 함수로 바꾸는 예 

```py
# authz.rego
package authz

import rego.v1

replace_rule if {
	replace(input.label)
}

replace(label) if {
	label == "test_label"
}
```

```py
# authz_test.rego
package authz_test

import rego.v1

import data.authz

test_replace_rule if {
    # authz.replace_rule을 테스트할 때 input.label 값을 "does-not-matter"로 설정
    # replace 함수를 true로 대체하여 테스트 조건을 설정
    # 이 테스트는 replace_rule이 true로 평가되는지 확인
	authz.replace_rule with input.label as "does-not-matter" with replace as true
}
```

```bash
$ opa test -v authz.rego authz_test.rego
data.authz_test.test_replace_rule: PASS (648.314µs)
--------------------------------------------------------------------------------
PASS: 1/1
```

이 테스트는 `replace_rule`이 예상대로 동작하는지 확인한다. `replace` 함수를 `true`로 대체해 규칙이 특정 조건에서 참인지 검증하여 성공적으로 테스트가 통과되었다.



## Policy Performance

### High Performance Policy Decisions



## External Data

## Integrating OPA

## Extending OPA

## Debugging OPA

## REST API

## CLI

## Intermediate Representation (IR)

## WebAssembly

## OPA 1.0

